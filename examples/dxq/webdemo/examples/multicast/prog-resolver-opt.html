<html><head><title>resolver-opt</title></head>
<body><h1>Program for resolver-opt</h1>
<a href='1.html'>Back to main</a>
<pre><code>
(: A DNS resolver.

   If $x is the IP address of a nameserver,
   and $n is a hostname, then

     R:lookup($x,$n)

   should return all the address records for $n.
:)

module namespace R = "resolver";
import module namespace S = "dxq:dns" at "dns.xqi";

(: Push path expressions only :)
declare function R:lookup($x,$n) {
  &lt;rr&gt;{ 
      exec { $x } { S:RR()/a[@hostname=$n] },
      for $ns in exec { $x } { S:RR()/ns }
      for $a in exec { $x } { S:RR()/a }

      where $ns/@nameserver=$a/@hostname
      and fn:not($ns/@domain=".")
      and glx:dns-lt($ns/@domain,$n)
      return
      R:lookup($a/@address,$n)/a
  }&lt;/rr&gt;
};

(: Local Join :)
declare function R:lookup2($x,$n) {
  &lt;rr&gt;{ 
  let $rr := exec { $x } { S:RR() }
  return
  ($rr/a[@hostname=$n], 
      for $ns in $rr/ns, $a in $rr/a
      where $ns/@nameserver=$a/@hostname
      and fn:not($ns/@domain=".")
      and glx:dns-lt($ns/@domain,$n)
      return
      R:lookup2($a/@address,$n)/a)
  }&lt;/rr&gt;
};

(: The idea here to define a function that build all resource records
   at one site, then have optimization that allows us to query a specific
   hostname and only query necessary sites.  Doesn't work with current
   optimizations.

   There is a common paradigm here where data is partitioned across nodes
   hierarchically based on domain/some index, and we would like to
   automatically figure out what partition(s) to visit.
:)
declare function R:allRR($x,$domain) {
  let $rr := exec { $x } { S:RR() } return
  let $childRR := 
    for $ns in $rr/ns, $a in $rr/a
    where $ns/@nameserver=$a/@hostname
    and glx:dns-lt($domain,$ns/@domain)
    return R:allRR($a/@address,$ns/@domain)
  return
  let $rr1 := $rr/a[glx:dns-le($domain,@hostname)]
  return
  let $childrr1 := $childRR[glx:dns-le($domain,@hostname)]
  return ($rr1,$childrr1)
};
declare function R:lookupN($n) {
  R:allRR("198.41.0.4",".")[@hostname=$n]
};

(: Push join to one site :)
declare function R:lookup3($x,$n) {
  &lt;rr&gt;{ 
  exec { $x } { S:RR()/a[@hostname=$n] },
  for $a in 
    exec { $x } { 
      for $ns in S:RR()/ns,
          $a in S:RR()/a
      where $ns/@nameserver=$a/@hostname
      and fn:not($ns/@domain=".")
      and glx:dns-lt($ns/@domain,$n)
      return $a 
    }
   return
   R:lookup3($a/@address,$n)/a
  }&lt;/rr&gt;
};

(: Simulate pushing a tuple plan :)
declare function R:lookup4($x,$n) {
  &lt;rr&gt;{ 
  let $tuples := 
    exec { $x } { 
      (&lt;hosts&gt;{ S:RR()/a[@hostname=$n] }&lt;/hosts&gt;,
       for $ns in S:RR()/ns,
           $a in S:RR()/a
       where $ns/@nameserver=$a/@hostname
       and fn:not($ns/@domain=".")
       and glx:dns-lt($ns/@domain,$n)
       return $a)
    }
  return
   ($tuples/self::hosts/a,
    for $a in $tuples/self::a return
    R:lookup4($a/@address,$n)/a)
  }&lt;/rr&gt;
};

(: ---- test functions ---- :)
declare function R:test($x) {
  &lt;rr&gt;
    { exec { $x } { S:RR()/a } }
  &lt;/rr&gt;
};

declare function R:test1($x) {
  &lt;rr&gt;
    { for $ns in exec { $x } { S:RR()/ns }
      for $a in exec { $x } { S:RR()/a }
      return $a 
    }
  &lt;/rr&gt;
};

</code></pre></body></html>
